#!/usr/bin/env perl

# bird -- client for birdhouse

use 5.10.0;
use Mojo::UserAgent;
use Mojo::JSON;
use File::Basename qw/dirname/;
use Term::Size;
use Term::ANSIScreen qw/:color :cursor :screen/;
use List::Util qw/max/;
use warnings;
use strict;

my ($ua, $id, $j, $rows, $cols);
my $birds = [];
my $connect;
my $tx;
my $server; # host:port
my $ws_url; # ws://host:port/path

&main;

exit;

sub debug { $ua->log->debug(@_); }

sub init_birdhouse {
    $|=1;
    chdir dirname $0;
    -d 'log' or mkdir 'log' or die $!;
    $server = shift @ARGV or die "Usage $0 <host:port>\n";
    $server = "http://$server" unless $server=~/:\/\//;
    $ws_url = Mojo::URL->new($server);
    $ws_url->scheme("ws");
    $ws_url->path("door");
    $ua     = Mojo::UserAgent->new();
    $id     = $ENV{USER} || [getpwuid($<)]->[0];
    $j      = Mojo::JSON->new();
    $ua->log->path('./log/birdhouse.log');
    $ua->websocket_timeout( 60 * 60 * 60 );
}

sub init_screen {
    ($cols, $rows) = Term::Size::chars *STDOUT{IO};
    $Term::ANSIScreen::AUTORESET = 1;
    print color 'white on black';
    cls();
    locate 1, 1;
    setscroll 1, $rows - 3;
}

sub write_status {
    my %a = @_;
    savepos;
    locate $rows- 2, 1;
    print( ( color 'black on green' ), "birdhouse at $server" );
    print( ( color 'black on green'), " @$birds");
    clline;
    loadpos;
}

sub prompt {
    locate $rows - 1, 1;
    clline;
    print ( ( color 'yellow on black'), "$id> " );
    locate $rows - 1,  length("$id> ") + 1;
    print color 'white on black';
}

sub timestamp {
    print color 'green on black';
    print "[",scalar(localtime)=~/(\S+) \d+$/,"] ";
}

sub write_server_msg {
    my $msg = shift;
    savepos;
    locate $rows-3, 1;
    timestamp();
    print color 'cyan on black';
    say $msg;
    loadpos;
}

sub write_chat_msg {
    my ($who,$msg) = @_;
    savepos;
    locate $rows-3, 1;
    timestamp();
    print color 'yellow on black';
    my $max = max map length, @$birds;
    printf("%${max}s: ",$who);
    print color 'white on black';
    say $msg;
    loadpos;
}

sub init_connection {
    $connect = sub {
        my $c = shift;
        $tx = pop;
        write_server_msg("connecting to $ws_url");
        if ( my $e = $tx->error ) {
            debug "error (retry in 5 seconds): $e";
            write_server_msg($e." (retry in 5 seconds)");
            Mojo::IOLoop->singleton->timer( 5 => sub { $ua->websocket( $ws_url => $connect ); }
            );
            return;
        }
        $tx->send_message("hello, i am $id");
        $tx->on_message(
            sub {
                my ( $tx, $msg ) = @_;
                $msg = $j->decode($msg);
                if ( $msg->{id} eq 'server' ) {
                    if ($msg->{birds}) {
                        $birds = [ @{ $msg->{birds} } ];
                        write_status();
                    }
                    write_server_msg( $msg->{msg}) if $msg->{msg};
                }
                else {
                    write_chat_msg($msg->{id} ,$msg->{msg});
                }
            }
        );
        $tx->on_finish(
            sub {
                write_server_msg "lost connection to $ws_url";
                Mojo::IOLoop->singleton->timer(
                    2 => sub { $ua->websocket( $ws_url => $connect ); } );
            }
        );
        $SIG{INT} = $SIG{QUIT} = sub {
            $tx->finish if $tx->is_websocket && $tx->is_done;
            setscroll 1, $rows;
            locate $rows-1,1;
            print "\nbye!\n";
            exit;
        };
        write_server_msg("connected.");
        prompt();
    };
}

sub init_stdin {
    $ua->websocket( $ws_url => $connect );
    my $w = Mojo::IOLoop->singleton->iowatcher;
    $w->add(
        \*STDIN,
        on_readable => sub {
            my ( $watcher, $handle ) = @_;
            chomp( my $input = <$handle> );
            unless ( $tx && $tx->is_websocket ) {
                write_server_msg("write failed : no connection to $server");
                return;
            }
            $tx->send_message($input);
            prompt();
        }
    );
}

sub main {
    init_birdhouse();
    debug "starting";
    init_screen();
    init_connection();
    write_status();
    init_stdin();
    Mojo::IOLoop->singleton->start;
}

